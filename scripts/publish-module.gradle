apply plugin: 'maven-publish'
apply plugin: 'signing'

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    if (project.plugins.findPlugin("com.android.library")) {
        // For Android libraries
        from android.sourceSets.main.java.srcDirs
        from android.sourceSets.main.kotlin.srcDirs
    } else {
        // For pure Kotlin libraries, in case you have them
        from sourceSets.main.java.srcDirs
        from sourceSets.main.kotlin.srcDirs
    }
}

artifacts {
    archives androidSourcesJar
}

group = PUBLISH_GROUP_ID
version = PUBLISH_VERSION

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId PUBLISH_GROUP_ID
                artifactId PUBLISH_ARTIFACT_ID
                version PUBLISH_VERSION

                if (project.plugins.findPlugin("com.android.library")) {
                    from components.release
                } else {
                    from components.java
                }

                artifact androidSourcesJar
                //artifact javadocJar

                pom {
                    name = PUBLISH_ARTIFACT_ID
                    description = 'Google Pay Button for JetPack Compose'
                    url = 'https://github.com/google-pay/compose-pay-button'
                    licenses {
                        license {
                            name = 'The Apache Software License, Version 2.0'
                            url = 'https://github.com/google-pay/compose-pay-button/blob/main/LICENSE'
                        }
                    }
                    developers {
                        developer {
                          id = 'google'
                          name = 'Google'
                        }
                    }
                    scm {
                        connection = 'scm:git:github.com/google-pay/compose-pay-button.git'
                        developerConnection = 'scm:git:ssh://github.com/google-pay/compose-pay-button.git'
                        url = 'https://github.com/google-pay/compose-pay-button/tree/main'
                    }
                }
            }
        }
    }
}

signing {
    useInMemoryPgpKeys(
            rootProject.ext["signing.keyId"],
            rootProject.ext["signing.key"],
            rootProject.ext["signing.password"],
    )
    sign publishing.publications
}